 1  HIMEM: 32768
 2  IF  PEEK (32768) = 75 THEN 10
 5  PRINT : PRINT  CHR$ (4)"BRUN AMPERRWTS,A$8000"
 10  TEXT : HOME : HTAB 10: PRINT "BANK ST. MUSICWRITER"
 20  PRINT : HTAB 13: PRINT "FILE EXTRACTOR"
 30  PRINT : PRINT : PRINT 
 35  PRINT : PRINT "INSERT A BANK ST. MUSICWRITER DATA DISK IN DRIVE 1.": PRINT : PRINT 
 40  INPUT "DIRECTORY SECTOR TO READ (DECIMAL 0-15) : ";S
 50  IF S < 0 OR S > 15 THEN 40
 90 BUF = 8192
 100  & R(18,S,BUF)
 200  HOME : PRINT "FILES IN THIS SECTOR:": PRINT : PRINT 
 210  FOR I = 1 TO 5
 220  PRINT I;":";
 225 N$(I) = ""
 230  FOR J = 1 TO 8
 240 N$(I) = N$(I) +  CHR$ ( PEEK (BUF + 50 * (I - 1) + J))
 250  NEXT J
 255  PRINT N$(I);
 260 N1 =  PEEK (BUF + 50 * (I - 1) + 15):N2 =  PEEK (BUF + 50 * (I - 1) + 16)
 270 T(I) = N2 * 16 +  INT (N1 / 16)
 275 S(I) = N1 -  INT (N1 / 16) * 16
 280  HTAB 15: PRINT "TRACK ";T(I);: HTAB 25: PRINT "SECTOR ";S(I);
 285  PRINT 
 290  NEXT I
 300  PRINT : INPUT "CHOOSE NUMBER OF FILE TO LOAD: ";F
 310  IF F < 0 OR F > 5 THEN 300
 315  IF T(F) = 0 THEN 300
 350  PRINT : PRINT 
 375  HGR2 : HGR : VTAB 24
 400 T0 = T(F):S0 = S(F):B0 = BUF
 405 CT = 0
 410  PRINT "TRACK "T0;: HTAB 15: PRINT "SECTOR "S0
 415 CT = CT + 1
 420  & R(T0,S0,B0)
 430 N1 =  PEEK (B0 + 254):N2 =  PEEK (B0 + 255)
 440  IF N1 = 0 AND N2 = 0 THEN 500
 450 T0 = N2 * 16 +  INT (N1 / 16)
 460 S0 = N1 -  INT (N1 / 16) * 16
 470 B0 = B0 + 256
 480  GOTO 410
 500  TEXT : PRINT : PRINT "LOADING COMPLETE...": PRINT : PRINT CT" SECTOR(S)  "CT / 4"K"
 505 X =  PEEK (37)
 510  PRINT : PRINT "]BSAVE "N$(F)",A"BUF",L"CT * 256
 520  VTAB X - 1: END 
