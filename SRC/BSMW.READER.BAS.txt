 0  PRINT  CHR$ (4)"PREFIX /BSMWMID2/WORK": PRINT  CHR$ (4)"CAT"
 1  INPUT "BSMW FILE TO CONVERT:";FB$: PRINT  CHR$ (4)"VERIFY "FB$
 2 NO$ = "CBAGFEDCBAGFEDCBAGFEDCBAGFEDC": TEXT : HOME : PRINT  CHR$ (21): HGR2 
 3  DIM SQ(6),BS(6),BY(6),RP(7,100): FOR I = 1 TO 6:BS(I) = 16384 + 512 * (I - 1):SQ(I) = BS(I): NEXT 
 4  FOR I = 1 TO 6: POKE SQ(I),0: POKE SQ(I) + 1,143 + I: POKE SQ(I) + 2,0: POKE SQ(I) + 3,0: POKE SQ(I) + 4,128: POKE SQ(I) + 5,0:SQ(I) = SQ(I) + 6: NEXT 
 5  DIM L(30),K(30),A(30): REM  TABLES TO TRANSLATE LINE NUMBERS TO MIDI NOTE NUMBERS
 6  FOR L = 1 TO 29: READ L(L):K(L) = L(L):A(L) = L(L): NEXT 
 7  FOR S = 1 TO 7: READ S(S): NEXT 
 8  FOR F = 1 TO 7: READ F(F): NEXT 
 9  DIM PT(6),PN(6),TT(6)
 10 BUF = 20480:LG$ = "/BSMWLOG/" + FB$ + ".TXT":MP$ = "/BSMWLOG/" + FB$ + ".MAP"
 11 R% = 0:RP(0,R%) = 0: FOR PT = 1 TO 6:RP(PT,R%) = 4: NEXT :R% = R% + 1
 14  PRINT "WRITING LOG TO "LG$
 15  PRINT  CHR$ (4)"OPEN "LG$: PRINT  CHR$ (4)"WRITE "LG$
 16  PRINT "BANK STREET MUSIC WRITER CONVERSION LOG:" + FB$
 17  PRINT  CHR$ (4)"CLOSE"
 20 PU = 0
 50  PRINT  CHR$ (4)"BLOAD "FB$",A"BUF",B"PU",L256"
 51  PRINT "LOADING: "FB$",A"BUF",B"PU",L256"
 52  PRINT  CHR$ (4)"OPEN"LG$: PRINT  CHR$ (4)"APPEND"LG$
 55  PRINT "LOADED "FB$",A"BUF",B"PU",L256"
 60  IF  PEEK (BUF + 254) = 0 AND  PEEK (BUF + 255) = 0 THEN DJ = 1
 100  FOR I = 0 TO 18
 150  PRINT "POSN "I
 200  FOR P = 1 TO 6
 250  PRINT P":";
 300 STS =  PEEK (BUF + (P - 1) * 38 + I)
 400 DTA =  PEEK (BUF + (P - 1) * 38 + I + 19)
 450  IF STS = 0 AND DTA = 0 THEN 700
 500  IF STS = 0 OR DTA = 160 THEN  GOSUB 10000: GOTO 700
 505  IF STS = 9 THEN  PRINT "PIANO";: GOTO 550
 506  IF STS = 11 THEN  PRINT "FORTE";: GOTO 550
 510 NUM = STS: GOSUB 1000: REM CONVERT NUMBER TO BIT ARRAY 
 514 DS = STS -  INT (STS / 8) * 8
 515 DU = 7.5 * 2 ^ DS
 520  IF BIT(7) THEN  PRINT "TRIPLET ";:DU = DU * 2 / 3
 521 TY = 0: IF BIT(6) THEN  PRINT "TIED ";:TY = 1
 522  IF BIT(5) THEN  PRINT "DOTTED ";:DU = DU * 3 / 2
 523  PRINT  MID$ ("????????32ND16TH8TH QTR HALFWHOL",(STS -  INT (STS / 8) * 8) * 4 + 1,4);
 524  PRINT " DUR="DU"  ";:TT(P) = TT(P) + DU
 525 L = DTA -  INT (DTA / 32) * 32
 526  IF BIT(4) THEN  PRINT "NOTE ";:RE = 0
 527  IF  NOT BIT(4) THEN  PRINT "REST ";:RE = 1
 528  IF BIT(3) THEN  PRINT "STEM UP ";
 529  IF  NOT BIT(3) THEN  PRINT "STEM DOWN ";
 530 NUM = DTA: GOSUB 1000
 531 OU = 0: IF BIT(7) THEN  PRINT "OCTAVE UP ";:OU = 1
 532  IF  NOT BIT(6) AND BIT(5) THEN  PRINT "NATURAL ";:A(L) = L(L)
 533  IF BIT(6) AND  NOT BIT(5) THEN  PRINT "FLAT ";:A(L) = L(L) - 1
 534  IF BIT(6) AND BIT(5) THEN  PRINT "SHARP ";:A(L) = L(L) + 1
 535 MN =  NOT (RE) * (A(L) + 12 * OU)
 540  PRINT "LINE #"L;" MIDI NOTE:"MN"  ";
 545  PRINT " "; MID$ (NO$,L,1);
 550  REM 
 600  IF PT(P) AND PN(P) = MN THEN  POKE SQ(P),0: POKE SQ(P) + 1,0: POKE SQ(P) + 2, INT (DU / 128) + 128: POKE SQ(P) + 3,DU -  INT (DU / 128) * 128:SQ(P) = SQ(P) + 4: GOTO 670
 610  POKE SQ(P),PN(P): POKE SQ(P) + 1,0
 620  POKE SQ(P) + 2,128: POKE SQ(P) + 3,0
 630  POKE SQ(P) + 4,MN: POKE SQ(P) + 5,64
 640  POKE SQ(P) + 6, INT (DU / 128) + 128: POKE SQ(P) + 7,DU -  INT (DU / 128) * 128
 650 SQ(P) = SQ(P) + 8
 660 PN(P) = MN
 670 PT(P) = 0: IF TY THEN PT(P) = 1
 700  PRINT : NEXT : PRINT : NEXT 
 710  GOSUB 10100: GOSUB 10050
 750  IF DJ = 1 THEN  FOR Q = 1 TO 6: POKE SQ(Q),PN(Q): POKE SQ(Q) + 1,0: POKE SQ(Q) + 2,0: POKE SQ(Q) + 3,255: POKE SQ(Q) + 4,47: POKE SQ(Q) + 5,0:SQ(Q) = SQ(Q) + 6: NEXT 
 800  FOR Q = 1 TO 6: PRINT SQ(Q) <  > BS(Q)"SAVING ";FB$;".";Q - 1",A"BS(Q)",E"SQ(Q) - 1",B"BY(Q): NEXT : PRINT  CHR$ (4)"CLOSE"
 801  FOR Q = 1 TO 6
 805  IF SQ(Q) <  > BS(Q) THEN  PRINT  CHR$ (4)"BSAVE ";FB$;".";Q - 1",A"BS(Q)",E"SQ(Q) - 1",B"BY(Q)
 810  NEXT Q
 820  PRINT  CHR$ (4)"OPEN"LG$: PRINT  CHR$ (4)"APPEND"LG$
 850  FOR Q = 1 TO 6:BY(Q) = BY(Q) + SQ(Q) - BS(Q): NEXT 
 860  FOR Q = 1 TO 6:SQ(Q) = BS(Q): NEXT 
 870  IF DJ <  > 1 THEN PU = PU + 256: PRINT  CHR$ (4)"CLOSE": GOTO 50
 900 RP(0,R%) = 99: FOR PT = 1 TO 6:RP(PT,R%) = BY(PT) + SQ(PT) - BS(PT): NEXT 
 901  GOTO 950
 905  FOR R2 = 0 TO R%: FOR PT = 0 TO 6: IF PT = 0 THEN  PRINT RP(PT,R2);",";: GOTO 920
 910  PRINT RP(PT,R2) + 2 * (R2 > 0);: IF PT < 6 THEN  PRINT ",";
 920  NEXT PT: PRINT 
 930  NEXT R2
 935  GOSUB 31000
 940  RETURN 
 950  GOSUB 905: PRINT  CHR$ (4)"CLOSE"
 960  PRINT  CHR$ (4)"OPEN"MP$: PRINT  CHR$ (4)"WRITE"MP$
 970  GOSUB 905
 980  PRINT  CHR$ (4)"CLOSE": PRINT "REPEAT MAP:": GOSUB 905
 990  TEXT 
 999  END 
 1000  FOR YY = 0 TO 7:BIT(YY) = 0: NEXT 
 1100  IF NUM >  = 128 THEN BIT(7) = 1:NUM = NUM - 128
 1110  IF NUM >  = 64 THEN BIT(6) = 1:NUM = NUM - 64
 1120  IF NUM >  = 32 THEN BIT(5) = 1:NUM = NUM - 32
 1130  IF NUM >  = 16 THEN BIT(4) = 1:NUM = NUM - 16
 1140  IF NUM >  = 8 THEN BIT(3) = 1:NUM = NUM - 8
 1150  IF NUM >  = 4 THEN BIT(2) = 1:NUM = NUM - 4
 1160  IF NUM >  = 2 THEN BIT(1) = 1:NUM = NUM - 2
 1170  IF NUM >  = 1 THEN BIT(0) = 1:NUM = NUM - 1
 1180  RETURN 
 10000  IF DTA = 0 THEN  PRINT "-";
 10005  IF DTA <  > 160 THEN 10020
 10007 CT = STS -  INT (STS / 8) * 8
 10008  FOR Q = 1 TO 30:K(Q) = L(Q): NEXT 
 10010  IF DTA = 160 THEN  PRINT "KEY CHANGE: ";:NUM = STS: GOSUB 1000: PRINT STS -  INT (STS / 8) * 8;" ";: PRINT  MID$ ("SHFL",BIT(3) * 2 + 1,2);
 10012  IF BIT(3) = 0 THEN  FOR Q0 = CT TO 1 STEP  - 1: FOR Q = S(Q0) TO 30 STEP 7:K(Q) = L(Q) + 1: NEXT : NEXT 
 10014  IF BIT(3) = 1 THEN  FOR Q0 = CT TO 1 STEP  - 1: FOR Q = F(Q0) TO 30 STEP 7:K(Q) = L(Q) - 1: NEXT : NEXT 
 10015  FOR Q = 1 TO 30:A(Q) = K(Q): NEXT 
 10019  GOTO 10049
 10020  IF DTA = 128 THEN  PRINT "BAR ";
 10025  FOR Q = 1 TO 30:A(Q) = K(Q): NEXT 
 10030  IF DTA = 192 THEN  PRINT "LRPT ";:RP(0,R%) = 1
 10035  IF DTA = 224 THEN  PRINT "RRPT ";:RP(0,R%) = 2
 10040  IF DTA = 136 THEN  PRINT "1ST END ";:RP(0,R%) = 3
 10045  IF DTA = 232 THEN  PRINT "2ND END ";:RP(0,R%) = 4
 10047  IF DTA <  > 128 THEN  PRINT " [BY="BY(P)" SQ="SQ(P)" BS="BS(P)"]"
 10048  IF DTA <  > 128 THEN RP(P,R%) = BY(P) + SQ(P) - BS(P): IF P = 6 THEN R% = R% + 1
 10049  RETURN 
 10050  FOR Q = 1 TO 6: IF DF(Q) = 0 THEN 10090
 10070 DW = DF(Q)
 10080  POKE SQ(Q),0: POKE SQ(Q) + 1,0:SQ(Q) = SQ(Q) + 2
 10085  POKE SQ(Q), INT (DW / 128) + 128: POKE SQ(Q) + 1,DW -  INT (DW / 128) * 128:SQ(Q) = SQ(Q) + 2
 10090  NEXT Q
 10099  RETURN 
 10100 MX = 0: FOR Q = 1 TO 6: IF TT(Q) > MX THEN MX = TT(Q)
 10110  NEXT Q
 10120  FOR Q = 1 TO 6: IF TT(Q) < MX THEN DF(Q) = MX - TT(Q)
 10125 TT(Q) = 0
 10130  NEXT Q
 10140  RETURN 
 20000  DATA  84,83,81,79,77,76,74
 20010  DATA  72,71,69,67,65,64,62
 20020  DATA   60,59,57,55,53,52,50   
 20030  DATA  48,47,45,43,41,40,38
 20040  DATA  36
 20050  DATA  5,1,4,7,3,6,2
 20055  DATA  2,6,3,7,4,1,5
 30000 HD$ = "0123456789ABCDEF":HX$ = ""
 30001  REM  SET DX=DECIMAL VALUE TO CONVERT
 30002  REM  RETURNS HX$
 30010 D% = DX: FOR EX = 3 TO 0 STEP  - 1:DV = 16 ^ EX
 30020 H% =  INT (D% / DV):D% = D% - H% * DV:HX$ = HX$ +  MID$ (HD$,H% + 1,1): NEXT 
 30030  RETURN 
 31000  FOR R2 = 0 TO R%: FOR PT = 0 TO 6: IF PT = 0 THEN  PRINT RP(PT,R2);",";: GOTO 31015
 31010 DX = BS(PT) + RP(PT,R2) + 2 * (R2 > 0): GOSUB 30000: PRINT HX$;: IF PT < 6 THEN  PRINT ",";
 31015  NEXT PT
 31016  PRINT 
 31020  NEXT R2
 31030  RETURN 
